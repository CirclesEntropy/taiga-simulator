module Simulator.PartialTx;

import Stdlib.Prelude open;
import Simulator.Resource open;

--- TODO: There should be exactly 2 created and 2 consumed resources.
type PartialTx :=
  | --- A partial transaction consists of a list of consumed resources and a list
    --- of created resources
    partialTx : List Resource -> List Resource -> PartialTx;

consumedResources : PartialTx -> List Resource;
consumedResources (partialTx rs _) := rs;

createdResources : PartialTx -> List Resource;
createdResources (partialTx _ rs) := rs;

module PartialTxTraits;
  import Stdlib.Trait.Ord open using {Ord as OrdTrait};

  Ord : Ord PartialTx;
  Ord :=
    let
      prod : PartialTx -> List Resource × List Resource;
      prod (partialTx consumedRs createdRs) :=
        consumedRs, createdRs;
      listResourceOrd : OrdTrait (List Resource);
      listResourceOrd := ListTraits.Ord ResourceTraits.Ord;
      prodOrd : OrdTrait (List Resource × List Resource);
      prodOrd :=
        ProductTraits.Ord listResourceOrd listResourceOrd;
      go : PartialTx -> PartialTx -> Ordering;
      go p1 p2 := cmp prodOrd (prod p1) (prod p2);
    in mkOrd go;
end;
